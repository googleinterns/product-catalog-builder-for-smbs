apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.googleinterns.smb"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        // Read the API key from ApiDemos/java/secure.properties into R.string.maps_api_key
        def secureProps = new Properties()
        if (file("../secure.properties").exists()) {
            file("../secure.properties")?.withInputStream { secureProps.load(it) }
        }
        resValue "string", "maps_api_key", (secureProps.getProperty("MAPS_API_KEY") ?: "")
        resValue "string", "directions_api_key", (secureProps.getProperty("DIRECTIONS_API_KEY") ?: "")

        // To add your Maps API key to this project:
        // 1. Create a file smb/merchant-app/secure.properties
        // 2. Add this line, where YOUR_API_KEY is your API key:
        //        MAPS_API_KEY=YOUR_API_KEY
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    aaptOptions {
        noCompress "tflite"
    }
    packagingOptions {
        exclude 'META-INF/androidx.exifinterface_exifinterface.version'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.camera:camera-camera2:1.0.0-alpha04'
    implementation 'androidx.camera:camera-core:1.0.0-alpha04'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.media:media:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Firebase analytics
    implementation 'com.google.firebase:firebase-analytics:17.4.0'
    // ML Kit dependencies
    implementation 'com.google.firebase:firebase-ml-vision:24.0.3'
    // Barcode detection model
    implementation 'com.google.firebase:firebase-ml-vision-barcode-model:16.1.1'
    // Firestore
    implementation 'com.google.firebase:firebase-firestore:21.4.0'
    // FirebaseUI (for authentication)
    implementation 'com.firebaseui:firebase-ui-auth:6.2.1'
    // Firebase cloud messaging
    implementation 'com.google.firebase:firebase-messaging:20.2.0'
    // Glide for loading images
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    // Fuzzy search library
    implementation 'me.xdrop:fuzzywuzzy:1.3.1'
    // Google Maps SDK
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    // Google Maps Utils
    implementation 'com.google.maps.android:android-maps-utils:1.3.3'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    // Gson Converter
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    // Google Location
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    // Tool tip library
    implementation 'com.github.vihtarb:tooltip:0.2.0'
    // Easy permissions
    implementation 'pub.devrel:easypermissions:3.0.0'
    // Date range picker
    implementation "com.google.android.material:material:1.3.0-alpha01"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'org.jetbrains:annotations-java5:15.0'
}

apply plugin: 'com.google.gms.google-services'
